
Grammar for Lox (precdence lowest to highest)

program             -> decleration* EOF;
decleration         -> func_decl | var_decl | statement ;
func_decl           -> "fun" function ;
function            -> IDENTIFIER "(" parameteres? ")" block;
parameteres         -> IDENTIFIER ( "," IDENTIFIER)* ;
var_decl            -> "var" IDENTIFIER ( "=" expression )? ";" ;
statement           -> expr_statement | for_statement | if_statement| print_statement | return_statement | while_statement | block  ;
while_statement     -> "while" "(" expression ")" statement ;
for_statement       -> "for" "(" (var_decl | expr_statement | ";" ) expression? ";" expression? ")" statement;
block               -> "{" decleration* "}" ;
if_statement        -> "if" "(" expression ")" statement ("else" statement)?;
expr_statement      -> expression ";";
print_statement     -> "print" expression ";";
return_statement    -> "return" expression? ";" ;
comma_expression    -> expression ("," expression)*;
ternary             -> expression | expression "?" ternary ":" ternary;// example expr ? true expr : false expr
expression          -> assignment ;
assignment          -> IDENTIFIER "=" assignment | logic_or ;
logic_or            -> logic_and ("or" logic_and)* ;
logic_and           -> equality ("and" equality)* ;
equality            -> comparison (( "!=" | "==" )  comparison)* ; // allowing expression like a == b, a != b, a == b != c
comparison          -> term ((">" | ">=" | "<" | "<=") term)* ;
term                ->  factor (("+" | "-") factor)* ;// allow a + b, a - b, a + b / c
factor              ->  unary ( ("/" | "*") unary)* ;
unary               -> ("!", "-") unary | call ;
call                -> primary ( "(" arguments ")")* ;
arguments           -> expression ("," expression)* ;
primary             -> NUMBER | STRING | "true" | "false" | "nil" | "(" expression ")" | IDENTIFIER ;
